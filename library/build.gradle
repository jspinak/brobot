plugins {
    id 'org.springframework.boot' version '2.7.14'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
    id 'io.freefair.lombok' version '6.6.3'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.jspinak'
version '1.0.7'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation('com.sikulix:sikulixapi:2.0.5') {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    implementation 'ch.qos.logback:logback-classic' //:1.4.7'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    implementation 'com.squareup:javapoet:1.9.0'
    implementation "org.deeplearning4j:deeplearning4j-core:1.0.0-M1"
    implementation "org.nd4j:nd4j-native-platform:1.0.0-M1"
    implementation 'com.github.kwhat:jnativehook:2.2.2'
    implementation 'org.elasticsearch.gradle:build-tools:8.10.1'
    implementation 'org.elasticsearch.client:elasticsearch-rest-client:8.10.1'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-elasticsearch'
    implementation 'jakarta.json:jakarta.json-api:2.1.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'javax.persistence:javax.persistence-api:2.2'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'io.github.jspinak'
            artifactId = 'brobot'
            version = '1.0.7'
            from components.java

            pom {
                name = 'brobot'
                description = 'testable state-based automation'
                url = 'https://github.com/jspinak/brobot'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'MIT license'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'jspinak'
                        name = 'Joshua Spinak'
                        email = 'jspinak@hotmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git:github.com/jspinak/brobot.git'
                    developerConnection = 'scm:git"ssh//github.com:jspinak/brobot.git'
                    url = 'https://github.com/jspinak/brobot'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            //url = "https://s01.oss.sonatype.org/content/repositories/snapshots/" // for snapshots
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/" // for releases
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}